plugins {
    id 'com.android.application'
}

android {
    namespace 'io.github.divverent.aaaaxy'
    compileSdk 32

    defaultConfig {
        applicationId 'io.github.divverent.aaaaxy'
        minSdk 30  // Actual requirement is 21, but requiring 30 to avoid likely slow EOL devices.
        targetSdk 32
        def rootDir = new File("${project.rootDir}/../..")
        versionCode "scripts/version.sh android".execute(null, rootDir).text.trim().toInteger()
        versionName "scripts/version.sh semver".execute(null, rootDir).text.trim()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("${aaaaxy_keystore}")
            storePassword "${aaaaxy_keystore_password}"
            keyAlias "${aaaaxy_key_alias}"
            keyPassword "${aaaaxy_key_password}"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation files('libs/aaaaxy.aar')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}
